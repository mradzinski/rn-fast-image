buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0'
    }
}

apply plugin: "com.android.library"

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def _reactNativeVersion = safeExtGet("reactNative", "+")
def _compileSdkVersion = safeExtGet("compileSdkVersion", 26)
def _glideVersion = safeExtGet("glideVersion", "4.7.1")

android {
    compileSdkVersion _compileSdkVersion
    buildToolsVersion safeExtGet("buildToolsVersion", "26.0.3")

    sourceSets {
        main {
            java {
                if (safeExtGet("excludeAppGlideModule", false)) {
                    srcDir "src"
                    exclude "**/FastImageGlideModule.java"
                }
            }
        }
    }

    defaultConfig {
        minSdkVersion safeExtGet("minSdkVersion", 16)
        targetSdkVersion safeExtGet("targetSdkVersion", 26)
        versionCode 1
        versionName "1.0"
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    jcenter()
    google()
    maven {
        url "https://maven.google.com"
    }
}

dependencies {
    //noinspection GradleDynamicVersion
    compileOnly fileTree( dir: "libs", includes: ['*.jar', '*.aar'] )

    annotationProcessor "com.github.bumptech.glide:compiler:${_glideVersion}"

    compileOnly "com.android.support:support-v4:${safeExtGet("supportLibVersion", "27.1.1")}"

    compileOnly("com.github.bumptech.glide:glide:${_glideVersion}") {
        exclude group: "com.android.support", module: "glide"
    }

    compileOnly("com.github.bumptech.glide:annotations:${_glideVersion}") {
        exclude group: "com.android.support", module: "annotations"
    }

    compileOnly("com.github.bumptech.glide:okhttp3-integration:${_glideVersion}") {
        exclude group: "com.android.support", module: "okhttp3-integration"
        exclude group: "com.android.support", module: "glide-parent"
    }
}
